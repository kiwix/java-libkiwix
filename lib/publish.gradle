apply plugin: 'maven-publish'
apply plugin: 'signing'

def siteUrl = 'https://www.kiwix.org/en/'
def gitUrl = 'https://github.com/kiwix/libkiwix.git'

group = GROUP_ID
version = VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId GROUP_ID
                artifactId ARTIFACT_ID
                version VERSION

                from components.release

                pom {
                    name = ARTIFACT_ID
                    description = 'LibKiwix Android library'
                    url = siteUrl
                    licenses {
                        license {
                            name = 'GPLv3'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                        }
                    }
                    developers {
                        developer {
                            id = 'kiwix'
                            name = 'kiwix'
                            email = 'contact@kiwix.org'
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
        repositories {
            maven {
                url = uri("https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/")
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            keyId,
            key,
            password,
    )
    sign publishing.publications
}